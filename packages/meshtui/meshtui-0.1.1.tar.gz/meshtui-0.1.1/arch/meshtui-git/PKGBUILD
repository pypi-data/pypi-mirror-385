# Maintainer: Emiel Kollof <emiel@kollof.nl>
pkgname=meshtui-git
pkgver=r0.0.0.0
pkgrel=1
pkgdesc="Terminal User Interface for MeshCore companion radios (development version)"
arch=('any')
url="https://github.com/ekollof/meshtui"
license=('MIT')
depends=(
    'python>=3.10'
    'python-meshcore>=2.1.9'
    'python-textual>=0.50.0'
    'python-requests>=2.28.0'
    'python-aiohttp>=3.9.0'
    'python-notify2>=0.3.1'
    'python-dbus>=1.2.18'
)
makedepends=(
    'git'
    'python-build'
    'python-installer'
    'python-wheel'
    'python-hatchling'
)
provides=('meshtui')
conflicts=('meshtui')
source=("git+https://github.com/ekollof/meshtui.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/meshtui"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/meshtui"
    python -m build --wheel --no-isolation
}

package() {
    cd "${srcdir}/meshtui"
    python -m installer --destdir="${pkgdir}" dist/*.whl

    # Install license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install documentation
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
